plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'io.morethan.jmhreport' version '0.7.0'
	id 'me.champeau.gradle.jmh' version '0.5.0'
	id 'java'
}
group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}
repositories {
	mavenCentral()
}
sourceSets {
	jmh {
		java.srcDirs = ['src/jmh/java']
		//resources.srcDirs = ['src/jmh/resources']
		compileClasspath += sourceSets.main.runtimeClasspath
	}
}
dependencies {
	compile 'org.json:json:20171018'

	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile 'com.itextpdf:itextg:5.5.9'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.10.3'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
	implementation 'com.google.guava:guava:28.2-jre'
	implementation 'com.flipkart.zjsonpatch:zjsonpatch:0.4.8'
	implementation 'net.sf.json-lib:json-lib:2.4:jdk15'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'com.h2database:h2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	jmhImplementation 'org.openjdk.jmh:jmh-core:1.21'
	jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.21'
}
test {
	useJUnitPlatform()
}

jmh {
	include = ['.*collection.*Benchmark']
	resultFormat = 'JSON'
//    synchronizeIterations = true
	duplicateClassesStrategy = 'warn'
}
task deleteEmptyBmList(type: Delete) {
	delete "$buildDir/jmh-generated-classes/META-INF/BenchmarkList"
}
jmhCompileGeneratedClasses.finalizedBy deleteEmptyBmList
tasks.jmh.finalizedBy tasks.jmhReport
classes.finalizedBy(jmhClasses)


